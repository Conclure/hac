plugins {
    id 'base'
    id 'idea'
    id 'net.ltgt.errorprone' version '1.3.0' apply false
    id 'org.sonarqube' version '3.0'
}

ext {
    /* Group name*/
    TASK_GROUP_NAME = 'hac package'

    /* ine latest spigot API for subprojects */
    LATEST_SPIGOT = 'org.spigotmc:spigot-api:1.16.4-R0.1-SNAPSHOT'

    /* Spigot jar version */
    /* Place a compiled Spigot Jar in the put_spigot_jars_here/ folder with the specified names seen here to build implementation */
    /* You don't need all jar versions you just need the jar for the version you want to compile. */
    /* Jar names must be their nms version names eg: 1.8.8 = 1_8_R3, 1.9.4 = 1_9_R2 */

    /* To define a new version just add a new index to the array and add the module to settings.gradle */
    VERSIONS = [:]

    def tmpVersions = [
        [find: 'eight', replace: '1_8_R3'],
        [find: 'nine', replace: '1_9_R2'],
        [find: 'ten', replace: '1_10_R1'],
        [find: 'eleven', replace: '1_11_R1'],
        [find: 'twelve', replace: '1_12_R1'],
        [find: 'thirteen', replace: '1_13_R2'],
        [find: 'fourteen', replace: '1_14_R1'],
        [find: 'fifteen', replace: '1_15_R1'],
        [find: 'sixteen', replace: '1_16_R3']
    ]

    tmpVersions.each {
        v ->
            VERSIONS[v.replace] = [
                replacer     : v,
                spigotJar    : file(buildDir.parentFile.path + '/put_spigot_jars_here/' + v.replace + '.jar'),
                projectAppend: ':' + v.replace,
                include      : false
            ]
    }

    def buildAll = task('Build All') {
        group TASK_GROUP_NAME
    }

    VERSIONS.each {
        k, v ->
            task(k.toString()) {
                group TASK_GROUP_NAME
                doFirst {
                    v.include = true
                }
            }

            buildAll.dependsOn k.toString()
    }
}

group 'com.heretere'

/* This is used to change the version for all subprojects */
version '0.0.6'

sonarqube {
    properties {
        property "sonar.projectKey", "Heirteir-commits_HAC"
        property "sonar.organization", "heirteir-commits"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

/* Project wide dependencies */
allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots'
        }
    }

    /* Apply errorprone to all project sources */
    pluginManager.withPlugin('net.ltgt.errorprone') {
        dependencies {
//            errorprone 'com.google.errorprone:error_prone_core:2.4.0'
        }
    }

    /* Project wide dependencies */
    pluginManager.withPlugin('java-library') {
        dependencies {
            compileOnlyApi 'org.jetbrains:annotations:16.0.2'
        }

        compileJava {
            /* Insure that all versions are compiled with 1.8 support */
            sourceCompatibility = JavaVersion.VERSION_1_8
            targetCompatibility = JavaVersion.VERSION_1_8
        }
    }
}

subprojects {
    /* Project wide plugins */
    apply plugin: 'idea'
//    apply plugin: 'net.ltgt.errorprone'
}